<?xml version="1.0" encoding="ISO-8859-1"?>
<rule name="F002" last-updated="2007.11.06">

<title>File names should be well-formed</title>

<description>
<p>The source file names should be well-formed in the sense of their allowed maximum length and directory depth. Directory and file names should start with alphabetic character or underscore. In addition, directory names should not contain dots and file names can have only one dot.</p>
</description>

<parameters>
<param name="max-directory-depth" default="8">Maximum depth of the directory structure.</param>
<param name="max-dirname-length" default="31">Maximum length of the directory path component.</param>
<param name="max-filename-length" default="31">Maximum length of the leaf file name.</param>
<param name="max-path-length" default="100">Maximum length of the full path.</param>
</parameters>

<compliance>
<standard name="Boost" />
</compliance>

<script><![CDATA[# File names should be well-formed

set maxDirectoryDepth [getParameter "max-directory-depth" 8]
set maxDirnameLength [getParameter "max-dirname-length" 31]
set maxFilenameLength [getParameter "max-filename-length" 31]
set maxPathLength [getParameter "max-path-length" 100]

foreach fileName [getSourceFileNames] {
    if {[string length $fileName] > $maxPathLength} {
        report $fileName 1 "path name too long"
    }

    set dirDepth 0
    foreach dir [file split [file dirname $fileName]] {
        if {$dir == "/" || $dir == "." || $dir == ".."} {
            continue
        }

        incr dirDepth

        if {[string length $dir] > $maxDirnameLength} {
            report $fileName 1 "directory name component too long"
            break
        }

        set first [string index $dir 0]
        if {[string is alpha $first] == 0 && $first != "_"} {
            report $fileName 1 "directory name should start with alphabetic character or underscore"
            break
        }

        if {[string first "." $dir] != -1} {
            report $fileName 1 "directory name should not contain the dot"
            break
        }
    }

    if {$dirDepth >= $maxDirectoryDepth} {
        report $fileName 1 "directory structure too deep"
    }

    set leafName [file tail $fileName]
    if {[string length $leafName] > $maxFilenameLength} {
        report $fileName 1 "file name too long"
    }

    set first [string index $leafName 0]
    if {[string is alpha $first] == 0 && $first != "_"} {
        report $fileName 1 "file name should start with alphabetic character or underscore"
    }

    if {[llength [split $leafName .]] > 2} {
        report $fileName 1 "file name should not contain more than one dot"
    }
}
]]></script>

<test>

<source><![CDATA[@ test.cpp
// no test
]]></source>

<result><![CDATA[]]></result>

</test>
</rule>
